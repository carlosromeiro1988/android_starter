apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'jacoco'
apply from: 'continuous_integration/jacoco.gradle'
apply from: 'continuous_integration/deployment.gradle'

/**
 * Jacoco Gradle Block
 */
jacoco {
    toolVersion = "$jacoco_version"
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.vw.nano"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        // Specifies flavors dimensions.
        flavorDimensions "normal"

        debug {
            testCoverageEnabled = true
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        productFlavors {
            english {
                applicationIdSuffix ".en"
                versionNameSuffix "-en"
                versionCode getCurrentVersionCode(name)
            }
        }
    }

    testOptions {

        unitTests.returnDefaultValues = true
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }

    dataBinding {
        enabled = true
    }
}

// region LocalMethods
def getCurrentVersionCode(def flavor) {
    def versionCode = 0
    file("$projectDir/releases/${flavor.toLowerCase()}/version.properties").withInputStream { inputStream ->
        Properties versionProps = new Properties()
        versionProps.load(inputStream)
        versionCode = versionProps['version_code'].toInteger()
    }
    return versionCode
}
//endregion

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //Kotlin Libs
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.2.1'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.2.1"

    //Support Lib
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.0.0'

    //Android Jetpack
    implementation 'androidx.core:core-ktx:1.0.2'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.0.0'
    implementation 'androidx.navigation:navigation-ui-ktx:2.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'

    //Picasso
    implementation 'com.squareup.picasso:picasso:2.71828'

    //Data Binding
    implementation 'com.android.databinding:library:3.4.1'
    kapt 'com.android.databinding:compiler:3.4.1'

    //Koin
    implementation 'org.koin:koin-android:2.0.1'
    implementation 'org.koin:koin-android-viewmodel:2.0.1'

    //Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'

    //Tests
    testImplementation 'io.mockk:mockk:1.9.3'
    testImplementation 'junit:junit:4.12'

    //Jacoco
    testImplementation "org.jacoco:org.jacoco.agent:${rootProject.ext.jacoco_version}"
    testImplementation "org.jacoco:org.jacoco.ant:${rootProject.ext.jacoco_version}"

    //UI Test
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
